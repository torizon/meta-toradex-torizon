DESCRIPTION = "Generate mixed uEnv and extlinux bootloader config for use with ostree"
LICENSE = "MIT"

# Linux for Tegra (L4T) from NVIDIA is based on the UEFI edk2
# bootloader. Since ostree in Torizon does not support configuration
# for edk2, use a trick with mixed uEnv.txt and extlinux.conf format
# to store the boot parameters. The —Åonfig file name and its location
# are left as is customary in Torizon/ostree,
# i.e. /boot/loader/uEnv.txt, but content is mixed with the
# U-Boot-style variables, containing paths to the kernel, initrd, dtb
# images and bootargs, generated by ostree, and the extlinux
# statements. The edk2-nvidia bootloader is patched to support such
# format using the usual syntax for extlinux, but substituting variables
# from uEnv on the fly. The implementation of ostree remains unchanged,
# as if using the U-Boot bootloader with a regular uEnv.txt.

do_install () {
	install -d ${D}${libdir}/ostree-boot
  	cat <<EOF >> uEnv.txt
MENU TITLE L4T boot options
DEFAULT primary
LABEL primary
	MENU LABEL primary
	LINUX \${kernel_image}
	INITRD \${ramdisk_image}
	APPEND \${bootargs}
EOF
	install -m 0644 uEnv.txt ${D}${libdir}/ostree-boot/
}

PACKAGES = "ostree-uenv-extlinux-conf"
FILES:ostree-uenv-extlinux-conf = "${libdir}/ostree-boot/uEnv.txt"
